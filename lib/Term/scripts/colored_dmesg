#!/usr/bin/perl
use strict;
use Term::ExtendedColor;

open(my $fh, '-|', 'dmesg') or die($!);
chomp(my @data = <$fh>);
close($fh);

# Emulate say()
$\ = "\n";

# Enable autoreset
Term::ExtendedColor::autoreset(1);


for(@data) {
  $_ =~ s/(usb)/fg('green5', bg('bold', $1))/egi;
  $_ =~ s/(disconnect)/bg('green4', fg('bold', $1))/egi;

  # Disks detected
  if($_ =~ /(^sd.*)\[(.+)\](.+)/) {
    my $pre  = fg('green28', $1) . fg('bold', '[');
    my $disk = fg('green18', $2) . fg('bold', ']');
    my $post = fg('green28', $3);
    print "$pre$disk$post";
  }
  # Out of memory
  elsif($_ =~ /^(Out of memory): (.+) process (\d+) \((.+)\)/) {
    my $pre  = fg('reverse', $1) . ': ';
    my $kill = fg('bold', fg('green3', $2));
    my $pid  = fg('bold', fg('green4', $3));
    my $app  = fg('green1', $4);
    my $post = fg('reverse', $5);
    print "$pre$kill $pid ($app) $post";
  }
  elsif($_ =~ /^(Killed) (process) (\d+) \((.+)\)/) {
    my $pre  = fg('reverse', $1) . ': ';
    my $kill = fg('bold', fg('green3', $2));
    my $pid  = fg('bold', fg('green4', $3));
    my $app  = fg('green1', $4);
    my $post = fg('reverse', $5);
    print "$pre$kill $pid ($app) $post";
  }
  # segfault     app    PID
  elsif($_ =~ /^(.+)\[(\d+)\]: (segfault) (.+)/) {
    my $app  = fg('bold', $1);
    my $pid  = fg('bold', fg('reverse', fg('green28', $2)));
    my $seg  = fg('bold', $3);
    my $post = fg('green28', $4);

    print "$pid: $app - $seg $post";
  }

  elsif($_ =~ /(.+) (Direct-Access)\s+(.+)/) {
    my $pre   = fg('green28', $1);
    my $acc   = fg('green28', $2);
    my $model = fg('green20', fg('bold', $3));;

    print "$pre $acc $model";
  }

  # EXT4-fs sdh1 warning: maximal mount count reached, running e2fsck is
  # recommended
  #
  # and others
  elsif($_ =~ /(.+) \((.+)\): (.+)/) {
    my $fs   = fg('bold', $1);
    my $disk = fg('bold', fg('green20', $2));;
    my $post = fg('bold', $3);

    print "$fs $disk $post";
  }

  elsif($_ =~ /^TCP: Peer/) {
    print fg('italic', fg('green20',  $_));
  }
  elsif($_ =~ /^(ACPI): (.+)/) {
    print fg('bold', $1) . ": $2";
  }

  else {
    print fg('green3', $_);
  }
}

=pod

=head1 NAME

  colored-dmesg - colorize the dmesg output

=head1 DESCRIPTION

  colored_dmesg demonstrates the color functions exported by
  Term::ExtendedColor.

=head1 AUTHOR

Written by Magnus Woldrich

=head1 REPORTING BUGS

Report bugs to trapd00r@trapd00r.se

=head1 COPYRIGHT

Copyright (C) 2010 Magnus Woldrich

License GPLv2

=cut

